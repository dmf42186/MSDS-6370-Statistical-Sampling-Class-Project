
proc import datafile = "/home/sbhattacharyya1/Raw Data Sources/Sampling_proj_data.xlsx"
	out = housing
	dbms = xlsx replace;
	getnames = Yes;
run;

proc sort data = housing;
by HouseStyle;
run;

/********************* Stratified Sampling - Proportion Allocation **************************/

proc surveyselect data = housing method = srs out = propsample
	sampsize = (22, 2, 103, 1, 2, 64, 5, 9) seed = 100000;
	strata HouseStyle;
	title "Stratified Sample with Proportional Allocation";
run;

data strsizes;
informat HouseStyle $6.;
format HouseStyle $6.;
input HouseStyle $6. _total_;
datalines;
1.5Fin 154
1.5Unf 14
1Story 726
2.5Fin 8
2.5Unf 11
2Story 445
SFoyer 37
SLvl   65
;
run;

proc surveymeans data = propsample sum clm clsum total = strsizes
	mean sum clsum;
	var SalePrice;
	weight SamplingWeight;
	strata HouseStyle;
	title "Stratified Sample with Proportional Allocation";
run;

/********************* Stratified Sampling - Neyman Allocation **************************/

proc surveyselect data = housing method = srs out = neysample
	sampsize = (23, 0, 92, 2, 2, 78, 3, 8) seed = 100000;
	strata HouseStyle;
	title "Stratified Sample with Neyman Allocation";
run;

proc surveymeans data = neysample sum clm clsum total = strsizes
	mean sum clsum;
	var SalePrice;
	weight SamplingWeight;
	strata HouseStyle;
	title "Stratified Sample with Neyman Allocation";
run;

/********************* Simple Random Sampling **************************/

proc surveyselect data = housing method = srs n = 208 out = srssample;
	title "Simple Random Sample";
run;

proc surveymeans data = srssample total = 1460
	mean sum clsum;
	var SalePrice;
run;